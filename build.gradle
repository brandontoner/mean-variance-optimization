plugins {
    id 'java'
    id 'checkstyle'
    id 'jacoco'
}

group 'com.brandontoner'
version '0.1'

repositories {
    mavenCentral()
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        resources.srcDirs = ['src/jmh/resources']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '--add-modules' << 'jdk.incubator.vector'
}

tasks.withType(JavaExec) {
    jvmArgs('--add-modules', 'jdk.incubator.vector')
}

dependencies {
    // AWS
    implementation platform('software.amazon.awssdk:bom:2.17.46')
    implementation 'software.amazon.awssdk:s3'
    // DI
    implementation 'javax.inject:javax.inject:1'
    // Testing
    testImplementation(platform('org.junit:junit-bom:5.8.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.mockito:mockito-core:3.5.10')
    // JMH
    jmhImplementation 'org.openjdk.jmh:jmh-core:1.33'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.33'
}

test {
    jvmArgs('--add-modules', 'jdk.incubator.vector')
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

jacoco {
    toolVersion "0.8.6"
}

checkstyle {
    toolVersion '9.0'
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
    mainClass = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath
}

classes.finalizedBy(jmhClasses)
